plugins {
    id 'org.jenkins-ci.jpi' version '0.18.1'
}

description = 'Jenkins plugin to leverage job-dsl-core to programmatic create jobs from inside Jenkins.'

// keep the generated sources outside of the build directory, see http://stackoverflow.com/a/21003914/1271460
def generatedSourcesDir = 'generated'

jenkinsPlugin {
    coreVersion = jenkinsVersion
    displayName = 'Job DSL'
    url = 'https://wiki.jenkins-ci.org/display/JENKINS/Job+DSL+Plugin'
    gitHubUrl = 'https://github.com/jenkinsci/job-dsl-plugin'
    compatibleSinceVersion = '1.46'
    localizerOutputDir = "$generatedSourcesDir/localizer"
    disabledTestInjection = false
    developers {
        developer {
            id 'quidryan'
            name 'Justin Ryan'
            email 'jryan@netflix.com'
        }
        developer {
            id 'andrewharmellaw'
            name 'Andrew Harmel-Law'
            email 'andrew@harmel-law.com'
        }
        developer {
            id 'daspilker'
            name 'Daniel Spilker'
            email 'mail@daniel-spilker.com'
        }
        developer {
            id 'sheehan'
            name 'Matt Sheehan'
            email 'mr.sheehan@gmail.com'
        }
    }
}

clean {
    delete generatedSourcesDir
}

configurations {
    // workaround for JENKINS-32978
    compile.exclude group: 'org.kohsuke', module: 'windows-package-checker'
}

jpi {
    into('api-viewer') {
        from(project(':job-dsl-api-viewer').projectDir) {
            include 'index.html'
            include 'build/dist/**'
        }
    }
    dependsOn tasks.getByPath(':job-dsl-api-viewer:build')
}

dependencies {
    compile project(':job-dsl-core')
    compile 'org.jenkins-ci:symbol-annotation:1.1'
    jenkinsPlugins 'org.jenkins-ci.plugins:structs:1.1@jar'
    optionalJenkinsPlugins 'org.jenkins-ci.plugins:cloudbees-folder:4.4@jar'
    optionalJenkinsPlugins 'org.jenkins-ci.plugins:vsphere-cloud:1.1.11@jar'
    optionalJenkinsPlugins 'org.jenkins-ci.plugins:config-file-provider:2.8.1@jar'
    optionalJenkinsPlugins 'org.jenkinsci.plugins:managed-scripts:1.2.1@jar'
    jenkinsTest 'org.jenkins-ci.main:jenkins-test-harness:2.7@jar'
}
