buildscript {
    repositories {
        jcenter()
        maven {
            url 'http://repo.jenkins-ci.org/releases/'
        }
    }
    dependencies {
        classpath 'org.jenkins-ci.tools:gradle-jpi-plugin:0.10.1'
    }
}

apply plugin: 'org.jenkins-ci.jpi'

description = 'Jenkins plugin to leverage job-dsl-core to programmatic create jobs from inside Jenkins.'

// keep the generated sources outside of the build directory, see http://stackoverflow.com/a/21003914/1271460
def generatedSourcesDir = 'generated'

jenkinsPlugin {
    coreVersion = '1.554.2'
    displayName = 'Job DSL'
    url = 'https://wiki.jenkins-ci.org/display/JENKINS/Job+DSL+Plugin'
    gitHubUrl = 'https://github.com/jenkinsci/job-dsl-plugin'
    maskClasses = 'groovy.grape'
    compatibleSinceVersion = '1.29'
    pluginFirstClassLoader = true
    localizerOutputDir = "$generatedSourcesDir/localizer"
    developers {
        developer {
            id 'quidryan'
            name 'Justin Ryan'
            email 'jryan@netflix.com'
        }
        developer {
            id 'andrewharmellaw'
            name 'Andrew Harmel-Law'
            email 'andrew@harmel-law.com'
        }
        developer {
            id 'sit'
            name 'Emil Sit'
            email 'sit@hadapt.com'
        }
        developer {
            id 'daspilker'
            name 'Daniel Spilker'
            email 'mail@daniel-spilker.com'
        }
    }
}

clean {
    delete generatedSourcesDir
}

configurations {
    forceInclude {}
}

jpi {
    classpath += configurations.forceInclude
}

dependencies {
    compile project(':job-dsl-core')
    forceInclude "org.codehaus.groovy:groovy-all:${groovyVersion}"
    optionalJenkinsPlugins 'org.jenkins-ci.plugins:cloudbees-folder:4.4@jar'
    optionalJenkinsPlugins 'org.jenkins-ci.plugins:credentials:1.9.4@jar'
    optionalJenkinsPlugins 'org.jenkins-ci.plugins:vsphere-cloud:1.1.11@jar'
    optionalJenkinsPlugins 'org.jenkins-ci.plugins:config-file-provider:2.7@jar'
}
