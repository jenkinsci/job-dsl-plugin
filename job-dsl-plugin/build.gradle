buildscript {
    repositories {
        maven {
            url('https://repo.jenkins-ci.org/public/')
        }
    }
    dependencies {
        // see https://github.com/kohsuke/localizer/issues/12
        classpath 'org.jvnet.localizer:maven-localizer-plugin:1.23'
    }
}

plugins {
    id 'org.jenkins-ci.jpi' version '0.22.0'
}

description = 'Jenkins plugin to leverage job-dsl-core to programmatic create jobs from inside Jenkins.'

// keep the generated sources outside of the build directory, see http://stackoverflow.com/a/21003914/1271460
def generatedSourcesDir = 'generated'

jenkinsPlugin {
    coreVersion = jenkinsVersion
    displayName = 'Job DSL'
    url = 'https://wiki.jenkins-ci.org/display/JENKINS/Job+DSL+Plugin'
    gitHubUrl = 'https://github.com/jenkinsci/job-dsl-plugin'
    compatibleSinceVersion = '1.65'
    localizerOutputDir = "$generatedSourcesDir/localizer"
    disabledTestInjection = false
    developers {
        developer {
            id 'quidryan'
            name 'Justin Ryan'
            email 'jryan@netflix.com'
        }
        developer {
            id 'andrewharmellaw'
            name 'Andrew Harmel-Law'
            email 'andrew@harmel-law.com'
        }
        developer {
            id 'daspilker'
            name 'Daniel Spilker'
            email 'mail@daniel-spilker.com'
        }
        developer {
            id 'sheehan'
            name 'Matt Sheehan'
            email 'mr.sheehan@gmail.com'
        }
    }
    licenses {
        license {
            name 'Apache License, Version 2.0'
            url 'https://www.apache.org/licenses/LICENSE-2.0.txt'
        }
    }
}

clean {
    delete generatedSourcesDir
}

test {
    jvmArgs += ['-XX:MaxPermSize=256m']
    /* TODO useful in conjunction with BuildWatcher, but better to enable conditionally (in jpi plugin?) as in https://github.com/jenkinsci/plugin-pom/blob/d1c2e7dc75692014184260f6d975bac36b1123b1/pom.xml#L955-L965
    testLogging {
        showStandardStreams = true
    }
    */
}

configurations {
    // workaround for JENKINS-32978
    compile.exclude group: 'org.kohsuke', module: 'windows-package-checker'
}

war {
    into('api-viewer') {
        from(project(':job-dsl-api-viewer').projectDir) {
            include 'index.html'
            include 'build/dist/**'
        }
    }
    dependsOn tasks.getByPath(':job-dsl-api-viewer:build')
}

dependencies {
    compile(project(':job-dsl-core'))  {
        exclude group: 'org.jvnet.hudson', module:'xstream'
    }
    jenkinsPlugins 'org.jenkins-ci.plugins:structs:1.6@jar'
    jenkinsPlugins 'org.jenkins-ci.plugins:script-security:1.25@jar'
    optionalJenkinsPlugins 'org.jenkins-ci.plugins:vsphere-cloud:1.1.11@jar'
    optionalJenkinsPlugins 'org.jenkins-ci.plugins:config-file-provider:2.15.4@jar'
    optionalJenkinsPlugins 'org.jenkinsci.plugins:managed-scripts:1.3@jar'
    jenkinsTest 'org.jenkins-ci.plugins:cloudbees-folder:5.12@jar'
    jenkinsTest 'org.jenkins-ci.plugins:matrix-auth:1.3@jar'
    jenkinsTest 'org.jenkins-ci.plugins:nested-view:1.14@jar'
    jenkinsTest 'org.jenkins-ci.plugins:credentials:2.1.10@jar'
    jenkinsTest 'org.jenkins-ci.main:jenkins-test-harness:2.22@jar'
    jenkinsTest 'org.jenkins-ci.ui:jquery-detached:1.2.1:core-assets'
    testCompile('org.kohsuke:groovy-sandbox:1.10') { // workaround for JENKINS-35412
        exclude group: 'org.codehaus.groovy'
    }
}
