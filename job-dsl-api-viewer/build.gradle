import de.undercouch.gradle.tasks.download.Download

plugins {
    id 'com.moowork.gulp' version '0.10'
    id 'de.undercouch.download' version '2.0.0'
}

// see https://github.com/srs/gradle-gulp-plugin/issues/9
node {
    download = true
}

apply plugin: 'org.ajoberstar.github-pages'

githubPages {
    commitMessage = "updated gh-pages for $version"
    repoUri = "git@github.com:$githubUser/job-dsl-plugin.git"
    pages {
        from '.'
        include 'index.html'
        include 'build/data/**'
        include 'build/dist/**'
    }
}

task copyData(type: Copy) {
    from tasks.getByPath(':job-dsl-core:generateApiDoc')
    into "${project.buildDir}/data"
}

(38..44).each { minorVersion ->
    def version = "1.${minorVersion}"
    task "downloadData-${version}"(type: Download) {
        src "http://repo.jenkins-ci.org/releases/org/jenkins-ci/plugins/job-dsl-core/${version}/job-dsl-core-${version}-apidoc.json"
        dest "${project.buildDir}/data/dsl-${version}.json"
        onlyIfNewer true
    }
    tasks.gulp_build.dependsOn("downloadData-${version}")
}

tasks.gulp_build.dependsOn('npmInstall')
tasks.gulp_build.dependsOn('copyData')
tasks.publishGhPages.dependsOn('gulp_build')

task clean(type: Delete) {
    delete project.buildDir
}
