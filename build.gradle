buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.github.rholder:gradle-one-jar:1.0.3'
    }
    dependencies {
        classpath 'org.ajoberstar:gradle-git:0.6.5'
    }
}

project(':job-dsl-core') {
    // jpi plugin will install appropriate repos for job-dsl-plugin
    repositories {
        mavenCentral()
        maven {
            url('http://maven.jenkins-ci.org/content/repositories/releases')
        }
    }
}

defaultTasks 'build' // For BuildHive

subprojects {
    group = 'org.jenkins-ci.plugins'
    apply plugin: 'groovy'
    apply plugin: 'maven' // For artifactory publishing
    apply plugin: 'codenarc'

    sourceCompatibility = 1.5
    targetCompatibility = 1.5

    dependencies {
        compile 'org.codehaus.groovy:groovy-all:1.8.6'
        compile 'com.google.guava:guava:14.0.1'
        testCompile 'org.spockframework:spock-core:0.7-groovy-1.8'
        testCompile 'junit:junit-dep:4.10'
        testCompile 'cglib:cglib-nodep:2.2.2' // Used by Spock
        testCompile 'org.objenesis:objenesis:1.2' // Used by Spock
    }
    test {
        useJUnit() // Causes "failed to create temp file to extract class from jar into"
    }

    codenarc {
        toolVersion = '0.21'
        configFile = file("../config/codenarc/rules.groovy")
    }

    codenarcTest {
        configFile = file("../config/codenarc/rules-test.groovy")
    }
}

project(':job-dsl-core') {
    apply plugin: 'application'

    description = "Generates Jenkins jobs via a DSL"
    dependencies {
        compile 'commons-codec:commons-codec:1.8' // For perforce
        compile 'org.apache.ivy:ivy:2.2.0' // Groovy Optional dependency, needed for @Grab
        compile 'xmlunit:xmlunit:1.4' // For runtime use, not just for testing
        compile 'org.jenkins-ci:version-number:1.1'
    }
    jar {
        manifest {
            attributes('Main-Class': 'javaposse.jobdsl.Run')
        }
    }
    mainClassName = 'javaposse.jobdsl.Run'

    apply plugin: 'gradle-one-jar'

    task jarSources(type:Jar){
        from sourceSets.main.allSource
        classifier = 'sources'
    }

    task jarJavadocs (type: Jar, dependsOn: 'javadoc') {
        from project.javadoc.destinationDir
        classifier = 'javadoc'
    }

    task jarGroovydocs (type: Jar, dependsOn: 'groovydoc') {
        from project.groovydoc.destinationDir
        classifier = 'groovydoc'
    }

    task oneJar(type: OneJar) {
        mainClass = project.mainClassName
    }

    artifacts {
        archives jarJavadocs
        archives jarGroovydocs
        archives jarSources
        archives oneJar
    }

    run {
        if ( project.hasProperty('args') ) {
            args project.args.split('\\s+')
        }
    }

    // gradle-jpi plugin does this for plugins, so we're mimicing it to get our additional artifacts up to repo.jenkinsci.org
    gradle.taskGraph.whenReady { taskGraph ->
        if ( taskGraph.hasTask(uploadArchives) ) {
            uploadArchives {
                repositories {
                    mavenDeployer {
                        def credentials = loadCredentialsForJenkinsCommunityRepository()
                        repository(url: 'http://maven.jenkins-ci.org:8081/content/repositories/releases') {
                            authentication(credentials)
                        }
                        snapshotRepository(url: 'http://maven.jenkins-ci.org:8081/content/repositories/snapshots') {
                            authentication(credentials)
                        }
                    }
                }
            }
        }
    }

}

private Map loadCredentialsForJenkinsCommunityRepository() {
    Properties props = new Properties()
    def dot = new File(System.getProperty("user.home"), ".jenkins-ci.org")

    if ( ! dot.exists() ) {
        throw new Exception("Trying to deploy to Jenkins community repository but there's no credential file ${dot}. See https://wiki.jenkins-ci.org/display/JENKINS/Dot+Jenkins+Ci+Dot+Org")
    }

    dot.withInputStream { i -> props.load(i) }
    return [userName: props["userName"], password: props["password"]]
}


project(':job-dsl-plugin') {
    description = "This plugin allows definition of Jobs via a DSL, using existing Jobs as a template."
    dependencies {
        compile project(':job-dsl-core')
    }
}

task wrapper(type: Wrapper) { gradleVersion = '1.8' }

apply plugin: 'github-pages'

githubPages {
    repoUri = 'git@github.com:jenkinsci/job-dsl-plugin.wiki.git'
    targetBranch = 'master'
    workingPath = "$buildDir/wiki"
    pages {
        from 'docs'
    }
}

commitGhPages.message = "updated wiki for $version"

// note: deleting pages is currently not supported
task publishDocs(dependsOn: publishGhPages)
